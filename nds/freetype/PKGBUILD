# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=nds-freetype
pkgver=2.13.0
pkgrel=1
pkgdesc='FreeType is a freely available software library to render fonts.'
arch=('any')
url='https://www.freetype.org/'
license=(GPL2+ custom:FreeType)
options=(!strip libtool staticlibs)
depends=("nds-bzip2"
         "nds-libpng"
         "nds-zlib"
)
makedepends=('ppc-pkg-config' 'dkp-toolchain-vars')
source=("http://download.savannah.gnu.org/releases/freetype/freetype-$pkgver.tar.xz")
sha256sums=(
  '5ee23abd047636c24b2d43c6625dcafc66661d1aca64dec9e0d05df29592624c'
)
groups=("nds-portlibs")

build() {
  cd freetype-$pkgver

  source "$DEVKITPRO"/ndsvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
    --enable-freetype-config \
    --disable-shared --enable-static \
    --with-zlib \
    --with-bzip2 \
    --with-png

  make
}

package() {
  cd freetype-$pkgver

  source "$DEVKITPRO"/ndsvars.sh

  make DESTDIR="$pkgdir" install

  # patch the config script to always output static libraries when not relying on pkg-config
  sed 's/\$show_static/yes/' -i "${pkgdir}/${PORTLIBS_PREFIX}/bin/freetype-config"

  # Licenses
  install -Dm644 "LICENSE.TXT" "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/LICENSE.TXT"
  install -Dm644 "docs/GPLv2.TXT"   "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/GPLv2.TXT"
  install -Dm644 "docs/FTL.TXT"     "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/FTL.TXT"

  # remove useless stuff
  rm -r "${pkgdir}/${PORTLIBS_PREFIX}/share"
}
