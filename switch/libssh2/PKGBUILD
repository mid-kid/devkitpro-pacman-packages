# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=switch-libssh2
pkgver=1.10.0
pkgrel=2
pkgdesc="A library implementing the SSH2 protocol as defined by Internet Drafts"
url="https://github.com/libssh2/libssh2"
license=('BSD')
arch=('any')
options=(!strip libtool staticlibs)
depends=('switch-mbedtls' 'switch-zlib')
makedepends=('dkp-toolchain-vars')
groups=('switch-portlibs')
source=("${url}/releases/download/libssh2-${pkgver}/libssh2-${pkgver}.tar.gz" "disable-tests.patch")
sha256sums=(
  '2d64e90f3ded394b91d3a2e774ca203a4179f69aebee03003e5a6fa621e41d51'
  '829c4a08da7d29cd975e1837882cfe2defcb6f181bb10059bf19936fd3a19196'
)

prepare() {
  cd "libssh2-${pkgver}"
  patch -Np1 -i "$srcdir/disable-tests.patch"
}

build() {
  cd "libssh2-${pkgver}"

  source "$DEVKITPRO"/switchvars.sh

  ./configure --prefix=${PORTLIBS_PREFIX} \
	--disable-shared --enable-static \
	--host=aarch64-none-elf \
	--with-crypto=mbedtls \
	--disable-examples-build \
	--disable-tests-build

  make
}

package() {
  cd "libssh2-${pkgver}"

  source "$DEVKITPRO"/switchvars.sh

  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}$DEVKITPRO/portlibs/switch/licenses/${pkgname}/LICENSE"
}
