# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: rsn8887 <raist66676@gmx.de>

pkgname=switch-libsodium
pkgver=1.0.18
pkgrel=1
pkgdesc='A modern and easy-to-use crypto library.'
arch=('any')
url='http://libsodium.org'
license=('custom')
options=(!strip libtool staticlibs)
makedepends=('dkp-toolchain-vars')
source=("https://download.libsodium.org/libsodium/releases/libsodium-$pkgver.tar.gz")
sha256sums=('6f504490b342a4f8a4c4a02fc9b866cbef8622d5df4e5452b46be121e46636c1')
groups=('switch-portlibs')

build() {
  cd libsodium-$pkgver

  source "$DEVKITPRO"/switchvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
    --disable-shared --enable-static
  make
}

package() {
  cd libsodium-$pkgver

  source "$DEVKITPRO"/switchvars.sh

  make DESTDIR="$pkgdir" install
  
  # license
  install -Dm644 LICENSE "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/LICENSE
}
