
# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=switch-curl
pkgver=7.69.1
pkgrel=5
pkgdesc='An URL retrieval utility and library'
arch=('any')
url='http://www.zlib.net/'
license=('zlib')
options=(!strip libtool staticlibs)
depends=('switch-zlib' 'libnx')
makedepends=('switch-pkg-config' 'dkp-toolchain-vars')
source=(
    "https://curl.haxx.se/download/curl-${pkgver}.tar.xz"
    'switch-curl.patch'
)
groups=('switch-portlibs')

sha256sums=(
 '03c7d5e6697f7b7e40ada1b2256e565a555657398e6c1fcfa4cb251ccd819d4f'
 '723c7d884fc7c39ae1a3115ba245bb8c1415da47bbd60ab8f943ca98f92ebc9a'
)

build() {
  cd curl-$pkgver

  patch -Np1 -i $srcdir/switch-curl.patch

  source "$DEVKITPRO"/switchvars.sh
  LDFLAGS="-specs=${DEVKITPRO}/libnx/switch.specs ${LDFLAGS}"

  # Don't execute the AC_RUN_IFELSE macro without parameters
  # error: cannot run test program while cross compiling
  sed -i -e '/AC_REQUIRE(\[AC_RUN_IFELSE\])/d' m4/curl-functions.m4

  ./buildconf

  ./configure --prefix=$PORTLIBS_PREFIX --host=aarch64-none-elf \
    --disable-shared --enable-static --disable-ipv6 --disable-unix-sockets \
    --disable-manual --disable-ntlm-wb --disable-threaded-resolver \
    --without-ssl --without-polar-ssl --without-cyassl --without-wolfssl \
    --without-mbedtls \
    --with-libnx \
    --with-default-ssl-backend=libnx

  make -C lib
  
}

package() {
  cd curl-$pkgver

  source "$DEVKITPRO"/switchvars.sh

  make DESTDIR="$pkgdir" -C lib install
  make DESTDIR="$pkgdir" -C include install
  make DESTDIR="$pkgdir" install-binSCRIPTS install-pkgconfigDATA

}
