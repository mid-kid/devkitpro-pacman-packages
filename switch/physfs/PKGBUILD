# Maintainer: Ezekiel Bethel <zek@9net.org>
_realname=physfs
pkgname=switch-${_realname}
pkgver=3.2.0
pkgrel=1
pkgdesc='A library to provide abstract access to various archives.'
arch=('any')
url='https://icculus.org/physfs/'
license=('zlib')
options=(!strip libtool staticlibs)
source=( "${_realname}-${pkgver}.tar.gz::https://github.com/icculus/physfs/archive/refs/tags/release-${pkgver}.tar.gz"
         "physfs-${pkgver}.patch")
makedepends=('devkitA64' 'dkp-toolchain-vars' 'switch-cmake')
sha256sums=(
  '1991500eaeb8d5325e3a8361847ff3bf8e03ec89252b7915e1f25b3f8ab5d560'
  '2a8f0d2fb09bdd534e0d3819842cf2861476d77e6464e6fdcedeb916e78acfad'
  )
groups=('switch-portlibs')
build() {
  cd physfs-release-$pkgver

  patch -Np1 -i "$srcdir"/physfs-${pkgver}.patch

  source ${DEVKITPRO}/switchvars.sh

  aarch64-none-elf-cmake -G"Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX="$DEVKITPRO"/portlibs/switch \
    -DPHYSFS_BUILD_STATIC=ON -DPHYSFS_BUILD_SHARED=OFF \
    -DPHYSFS_BUILD_TEST=OFF \
    .

  make
}

package() {
  cd physfs-release-$pkgver

  make install DESTDIR="$pkgdir"
}
