
# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=switch-zlib
pkgver=1.3
pkgrel=1
pkgdesc='Deflate compression method library'
arch=('any')
url='http://www.zlib.net'
license=('zlib')
options=(!strip libtool staticlibs)
source=("${url}/zlib-$pkgver.tar.xz")
sha256sums=(
  '8a9ba2898e1d0d774eca6ba5b4627a11e5588ba85c8851336eb38de4683050a7'
)
makedepends=('dkp-toolchain-vars')
groups=('switch-portlibs')

build() {
  source "$DEVKITPRO"/switchvars.sh
  cd zlib-$pkgver
  CFLAGS="-g -O2 -march=armv8-a -mtune=cortex-a57 -mtp=soft -fPIC -ftls-model=local-exec" \
  CHOST=aarch64-none-elf \
    ./configure --prefix="$DEVKITPRO/portlibs/switch" --static
  make libz.a # only build library, no programs

  # libminizip
  cd contrib/minizip
  autoreconf --force --verbose --install
  CFLAGS="${CFLAGS} -DUSE_FILE32API"
  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
    --disable-shared --enable-static
  make
}

package() {
  source "$DEVKITPRO"/switchvars.sh
  cd zlib-$pkgver
  # zlib
  make DESTDIR="$pkgdir" install
  # license
  install -d "$pkgdir$DEVKITPRO"/portlibs/switch/licenses/$pkgname
  tail -n 31 README > "$pkgdir$DEVKITPRO"/portlibs/switch/licenses/$pkgname/LICENSE
  # remove useless stuff
  rm -r "$pkgdir$DEVKITPRO"/portlibs/switch/share
  # libminizip
  cd contrib/minizip
  make DESTDIR="$pkgdir" install
}
