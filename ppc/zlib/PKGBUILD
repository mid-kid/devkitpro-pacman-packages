# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=ppc-zlib
pkgver=1.3
pkgrel=1
pkgdesc='Deflate compression method library'
arch=('any')
url='http://www.zlib.net'
license=('zlib')
options=(!strip libtool staticlibs)
source=("${url}/fossils/zlib-$pkgver.tar.gz")
makedepends=('dkp-toolchain-vars')
groups=('ppc-portlibs')
build() {
  cd zlib-$pkgver

  source "$DEVKITPRO"/devkitppc.sh
  source "$DEVKITPRO"/ppcvars.sh

  CHOST=powerpc-eabi \
    ./configure --prefix="$DEVKITPRO/portlibs/ppc" --static
  make libz.a # only build library, no programs
}

package() {
  cd zlib-$pkgver

  source "$DEVKITPRO"/devkitppc.sh

  make DESTDIR="$pkgdir" install
  # license
  install -d "$pkgdir$DEVKITPRO"/portlibs/ppc/licenses/$pkgname
  tail -n 31 README > "$pkgdir$DEVKITPRO"/portlibs/ppc/licenses/$pkgname/LICENSE
  # remove useless stuff
  rm -r "$pkgdir$DEVKITPRO"/portlibs/ppc/share
}
sha256sums=('ff0ba4c292013dbc27530b3a81e1f9a813cd39de01ca5e0f8bf355702efa593e')
