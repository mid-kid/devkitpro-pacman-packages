# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=ppc-libvorbisidec
pkgver=1.2.1
pkgrel=2
pkgdesc='Fixed-point version of the Ogg Vorbis decoder (for Nintendo Gamecube/wii homebrew development)'
arch=('any')
url="http://xiph.org"
license=('LGPL')
options=(!strip libtool staticlibs)
_ver=$pkgver+git20180316
source=("http://deb.debian.org/debian/pool/main/libv/libvorbisidec/libvorbisidec_$_ver.orig.tar.gz")
sha256sums=('491b88661544f55b17154407420b5d78d2ac35d12786ee030d0925eb6aec03da')
makedepends=('ppc-pkg-config' 'dkp-toolchain-vars')
depends=('ppc-libogg')
groups=('ppc-portlibs')
build() {
  cd libvorbisidec-$_ver

  source "$DEVKITPRO"/ppcvars.sh

  ACLOCAL_PATH="$PORTLIBS_PREFIX/share/aclocal:$ACLOCAL_PATH" \
  ./autogen.sh --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi

  make
}

package() {
  cd libvorbisidec-$_ver

  source "$DEVKITPRO"/ppcvars.sh

  make DESTDIR="$pkgdir" install
  # license
  install -Dm644 COPYING "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/COPYING
}

