# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=3ds-libvorbisidec
pkgver=1.2.1
pkgrel=3
pkgdesc='Fixed-point version of the Ogg Vorbis decoder (for Nintendo 3DS homebrew development)'
arch=('any')
url="http://xiph.org"
license=('LGPL')
options=(!strip libtool staticlibs)
_ver=$pkgver+git20180316
source=("http://deb.debian.org/debian/pool/main/libv/libvorbisidec/libvorbisidec_$_ver.orig.tar.gz")
sha256sums=('491b88661544f55b17154407420b5d78d2ac35d12786ee030d0925eb6aec03da')
makedepends=('3ds-pkg-config' 'dkp-toolchain-vars')
depends=('3ds-libogg')
groups=('3ds-portlibs')

build() {
  cd libvorbisidec-$_ver

  source "$DEVKITPRO"/devkitarm.sh
  source "$DEVKITPRO"/3dsvars.sh

  ACLOCAL_PATH="$PORTLIBS_PREFIX/share/aclocal:$ACLOCAL_PATH" \
  ./autogen.sh --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi

  make
}

package() {
  cd libvorbisidec-$_ver

  source "$DEVKITPRO"/devkitarm.sh
  source "$DEVKITPRO"/3dsvars.sh

  make DESTDIR="$pkgdir" install
  # license
  install -Dm644 COPYING "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/COPYING
}

