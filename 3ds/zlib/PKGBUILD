
# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=3ds-zlib
pkgver=1.3
pkgrel=1
pkgdesc='Deflate compression method library'
arch=('any')
url='http://www.zlib.net'
license=('zlib')
options=(!strip libtool staticlibs)
source=("${url}/fossils/zlib-$pkgver.tar.gz")
makedepends=('dkp-toolchain-vars')
groups=('3ds-portlibs')

build() {
  cd zlib-$pkgver

  source "$DEVKITPRO"/3dsvars.sh

  CHOST=arm-none-eabi \
    ./configure --prefix="${PORTLIBS_PREFIX}" --static
  make libz.a # only build library, no programs

  # libminizip
  cd contrib/minizip
  autoreconf --force --verbose --install
  CFLAGS="${CFLAGS} -DUSE_FILE32API"
  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
    --disable-shared --enable-static
  make
}

package() {
  cd zlib-$pkgver

  source "$DEVKITPRO"/3dsvars.sh

  make DESTDIR="$pkgdir" install
  # license
  install -d "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname
  tail -n 31 README > "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/LICENSE
  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share

  # libminizip
  cd contrib/minizip
  make DESTDIR="$pkgdir" install
}
sha256sums=('ff0ba4c292013dbc27530b3a81e1f9a813cd39de01ca5e0f8bf355702efa593e')
