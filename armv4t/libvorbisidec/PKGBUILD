# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=armv4t-libvorbisidec
pkgver=1.2.1
pkgrel=1
pkgdesc='Fixed-point version of the Ogg Vorbis decoder (for armv4t homebrew development)'
arch=('any')
url="http://xiph.org"
license=('LGPL')
options=(!strip libtool staticlibs)
_ver=$pkgver+git20180316
source=("http://deb.debian.org/debian/pool/main/libv/libvorbisidec/libvorbisidec_$_ver.orig.tar.gz")
sha256sums=('491b88661544f55b17154407420b5d78d2ac35d12786ee030d0925eb6aec03da')
makedepends=('dkp-toolchain-vars' 'armv4t-pkg-config')
depends=('armv4t-libogg')
groups=('armv4t-portlibs')

build() {
  cd libvorbisidec-$_ver

  source "$DEVKITPRO"/armv4tvars.sh

  ACLOCAL_PATH="$PORTLIBS_PREFIX/share/aclocal:$ACLOCAL_PATH" \
  ./autogen.sh --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi

  make
}

package() {
  cd libvorbisidec-$_ver

  source "$DEVKITPRO"/armv4tvars.sh

  make DESTDIR="$pkgdir" install
  # license
  install -Dm644 COPYING "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/COPYING
}

