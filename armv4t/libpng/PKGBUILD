# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=armv4t-libpng
pkgver=1.6.39
pkgrel=2
pkgdesc='PNG format graphic files library'
arch=('any')
url='http://www.libpng.org'
license=('custom')
options=(!strip libtool staticlibs)
depends=('armv4t-zlib')
groups=('armv4t-portlibs')
makedepends=('dkp-toolchain-vars')
source=("https://download.sourceforge.net/libpng/libpng-$pkgver.tar.xz")
sha256sums=(
  '1f4696ce70b4ee5f85f1e1623dc1229b210029fa4b7aee573df3e2ba7b036937'
)

build() {
  cd libpng-$pkgver

  source "$DEVKITPRO"/armv4tvars.sh

  sed -i 's/^bin_PROGRAMS = .*//' Makefile.in

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
    --disable-shared --enable-static
  make
}

package() {
  cd libpng-$pkgver

  source "$DEVKITPRO"/armv4tvars.sh

  make LN_S=cp DESTDIR="$pkgdir" install
  # license
  install -Dm644 LICENSE "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/LICENSE
  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}
