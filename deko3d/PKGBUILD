# Maintainer: fincs <fincs@devkitpro.org>

pkgname=('deko3d')
pkgver=0.4.0
pkgrel=1
pkgdesc="Homebrew low level graphics API for Nintendo Switch (Nvidia Tegra X1)"
arch=('any')
license=('custom')
url="http://github.com/devkitPro/${pkgname}"
options=(!strip libtool staticlibs)
_ver_dekotools=aebf6e299383668ff1d337ad6cb3daca0d4c3754
source=(${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz
    dekotools-$_ver_dekotools.tar.gz::https://github.com/fincs/dekotools/archive/$_ver_dekotools.tar.gz)
sha256sums=('f56af38ec84c008bd0141c203a72190b6bb91767030f34e4bd28c2b10951eb02'
            '63e90c920c42ec2d8708b7e15861417c601c8c6909ea7876f515a8b3723c85fc')
makedepends=('devkitA64')
depends=('libnx')

groups=('switch-dev')

build() {
  meson setup dekotools $srcdir/dekotools-$_ver_dekotools
  meson compile -C dekotools
  export PATH="$PWD/dekotools:$PATH"

  make -C $srcdir/$pkgname-$pkgver

}

package() {

  make -C $srcdir/$pkgname-$pkgver DESTDIR=$pkgdir install

}
